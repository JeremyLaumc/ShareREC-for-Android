apply plugin: 'com.android.library'

String findBuildTools() {
    def localProperties = new File(rootProject.projectDir, "local.properties")
    if (localProperties.exists()) {
        def properties = new Properties()
        localProperties.withInputStream {
            instr->properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        def buildTools = new File(sdkDir, "build-tools")
        if (buildTools.exists()) {
            def tools = buildTools.list()
            if (tools != null) {
                Arrays.sort(tools)
                return tools[tools.length - 1]
            }
        }
    }
    return "25.0.2"
}

String findShareRECVersionName() {
	def versionName = "2.0.0"
	def libs = new File(projectDir, "/libs")
	libs.list().each { name ->
		def upName = name.toUpperCase()
		if (upName.startsWith("SHAREREC-CORE-") && upName.endsWith(".JAR")) {
			versionName = name.substring("SHAREREC-CORE-".length(), name.length() - ".JAR".length())
			return
		}
	}
	return versionName
}

int findShareRECVersionCode() {
	def versionCode = 0
	findShareRECVersionName().split("\\.").each { v ->
		versionCode = versionCode * 100 + Integer.parseInt(v)
	}
	return versionCode == 0 ? 17 : versionCode
}

android {
    compileSdkVersion 21
    buildToolsVersion findBuildTools()

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode findShareRECVersionCode()
        versionName findShareRECVersionName()
    }

    sourceSets.main {
		manifest.srcFile 'AndroidManifest.xml'
        assets.srcDirs = ['assets']
        java.srcDirs = ['src']
        aidl.srcDirs = ['src']
        res.srcDirs = ['res']
        jniLibs.srcDirs = ['libs']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
}